* 安装外部命令
//ssc install winsor2
//ssc install asreg

* 指定数据所在路径
cd C:\Users\Alienware\Desktop\论文\论文操作数据

* 处理资产负债表
import excel FS_Combas.xlsx, firstrow clear 
drop if 报表类型 == "B" 
keep 证券代码 会计期间 应收账款净额 固定资产净额 资产总计 负债合计
export excel 资产负债表.xlsx, firstrow(variable) replace 
save 资产负债表.dta, replace

* 处理利润表
import excel FS_Comins.xlsx, firstrow clear 
drop if 报表类型 == "B" 
keep 证券代码 会计期间 营业总收入 利润总额 净利润
export excel 利润表.xlsx, firstrow(variable) replace
save 利润表.dta, replace 

* 处理现金流量表（直接法）
import excel FS_Comscfd.xlsx, firstrow clear 
drop if 报表类型 == "B" 
keep 证券代码 会计期间 经营活动产生的现金流量净额 
export excel 现金流量表.xlsx, firstrow(variable) replace 
save 现金流量表.dta, replace 

* 处理固定资产原值信息表
import excel 固定资产原值表.xlsx,firstrow clear
keep 证券代码 会计期间 固定资产原值
export excel 固定资产原值处理表.xlsx, firstrow(variable) replace 
save 固定资产原值处理表.dta, replace

* 处理上市公司基本信息年度表
import excel 上市公司基本信息年度表.xlsx,firstrow clear
keep 证券代码 会计期间 行业代码 首次上市日期 
export excel 上市公司基本信息年度处理表.xlsx, firstrow(variable) replace 
save 上市公司基本信息年度处理表.dta, replace

* 处理年末是否ST或PT信息表
import excel 年末是否ST或PT表.xlsx,firstrow clear
save 年末是否ST或PT处理表.dta, replace

* 处理退市公司名单信息表
import excel 退市公司名单表.xlsx,firstrow clear
save 退市公司名单处理表.dta, replace

* 处理资产减值准备表
import excel 资产减值准备表.xlsx,firstrow clear
drop if 报表类型 == "B"
gen year = substr(会计期间,1,4)
destring year,replace
destring 资产减值准备,replace
gen month = substr(会计期间,6,2)
drop if month == "01"
drop if month == "03"
drop if month == "06"
drop if month == "09"
keep 证券代码 year 资产减值准备
export excel 资产减值准备处理表.xlsx, firstrow(variable) replace 
save 资产减值准备处理表.dta, replace

* 合并报表信息得到初始数据表
use 资产负债表.dta,clear
merge 1:1 证券代码 会计期间 using 利润表.dta,keep(match) nogen
merge 1:1 证券代码 会计期间 using 现金流量表.dta,keep(match) nogen
merge 1:1 证券代码 会计期间 using 固定资产原值处理表.dta,keep(match) nogen
merge 1:1 证券代码 会计期间 using 上市公司基本信息年度处理表.dta,keep(match) nogen
gen Stkcd = 证券代码
destring Stkcd,replace
gen year = substr(会计期间,1,4)
destring year,replace
order Stkcd 证券代码 year
drop 会计期间
export excel 初始数据表.xlsx, firstrow(variable) replace
save 初始数据表.dta, replace

* 定义面版数据
xtset Stkcd year

* 剔除已退市的上市公司
merge m:1 Stkcd using 退市公司名单处理表.dta, nogen keep(1 3) keepusing(退市日期)
drop if 退市日期 !=.

* 加入资产减值准备信息
merge 1:1 证券代码 year using 资产减值准备处理表.dta,keep(match) nogen

* 剔除上市以前的数据
drop if year<real(substr(首次上市日期, 1, 4))

* 匹配当年是否ST、*ST或PT
merge 1:1 Stkcd year using 年末是否ST或PT处理表.dta, nogen keep(1 3 ) keepusing(年末是否ST或PT)

* 制造业使用二级分类，其他行业使用大类
//gen Industry=substr(行业代码, 1, 1)
//replace Industry=substr(行业代码, 1, 2) if Industry=="C"
gen Industry=行业代码

* 得到基础数据表
export excel 基础数据表.xlsx, firstrow(variable) replace
save 基础数据表.dta, replace

* 生成变量
xtset Stkcd year
gen TA=(净利润-经营活动产生的现金流量净额)/L.资产总计
gen A=1/L.资产总计
gen REV=D.营业总收入/L.资产总计
gen REV_REC=(D.营业总收入-D.应收账款)/L.资产总计
gen PPE=固定资产原值/L.资产总计

* 筛选数据
drop if 年末是否ST或PT==1  //剔除当年ST、PT类股票

drop if Industry=="J" //剔除金融业

foreach i in TA A REV REV_REC PPE{
	drop if `i'==.  //剔除有缺失值的变量
}

bysort Industry year:gen N = _N
drop if N < 10 //剔除行业观察值小于10的样本

* 开始计算并导出DA
winsor2 TA A REV PPE REV_REC, cuts(1 99) replace by(year) //缩尾处理

bys Industry year: asreg TA A REV PPE, nocons //分行业分年度回归

gen DA=TA-(_b_A*A+_b_REV*REV_REC+_b_PPE*PPE) //根据回归求DA

export excel DA_计算结果.xlsx, firstrow(variable) replace
save DA_计算结果.dta, replace

* 生成回归变量
xtset Stkcd year
gen AD=资产减值准备/(资产总计+资产减值准备) //资产减值净计提率
gen ROA=净利润/(资产总计/2+L.资产总计/2) //总资产收益率
gen ROE=净利润/((L.资产总计-L.负债合计)/2+(资产总计-负债合计)/2) //净资产收益率
gen LEV=负债合计/资产总计 //资产负债率
gen SIZE=ln(资产总计) //企业规模
gen NK = 0 if 净利润>=0 //是否亏损指标，如果净利润大于等于0表示未亏损
replace NK = 1 if 净利润 < 0 //净利润小于0表示确实亏损

*筛选数据并输出回归变量
foreach i in AD ROA ROE LEV SIZE NK 资产减值准备{
	drop if `i'==.  //剔除有缺失值的变量
}

keep if year>=2014 & year<=2020 //计算年份2000-2020
keep if Industry == "I65"
keep Stkcd year DA AD ROA ROE LEV SIZE NK
export excel 回归数据.xlsx, firstrow(variable) replace
save 回归数据.dta, replace

* 生成描述性统计信息
//ssc install outreg2
outreg2 using 描述性统计总表.doc, replace sum(log) keep(**DA** **AD** **ROA** **ROE** **LEV** **SIZE** **NK**) title(Descriptive statistion)
//bysort year:outreg2 using 描述性统计年度表.doc, replace sum(log) keep(**DA** **AD** **ROA** **ROE** **LEV** **SIZE** **NK**) title(Descriptive statistion)

* 相关性分析
ssc install logout
//logout, save (相关性分析)word replace: pwcorr DA AD ROA ROE LEV SIZE NK
//net install asdoc, from(http://fintechprofessor.com) replace
asdoc pwcorr DA AD ROA ROE LEV SIZE NK, star(all) nonum replace

*共线性分析
reg DA AD ROA ROE LEV SIZE NK
estat vif

	Note：不管有多少因变量，随便找一个因变量就可以，因为共线性分析只需要判断解释变量（X）之间的关系即可以，所以最后的结果都是解释变量之间的关系。
	Mean VIF 基本上小于10说明共线性不强。

* 多元回归分析
reg DA AD ROA ROE LEV SIZE NK
//outreg2 using xxx.doc, replace tstat bdec(3) tdec(2) ctitle(y)
outreg2 using 多元回归分析.doc, replace tstat bdec(3) tdec(2) e(r2_a,F) addstat(F test,e(p)) //加入F检验
/**
	//多模型回归
	reg DA AD ROA ROE LEV SIZE NK
	outreg2 using xxx.doc, append tstat bdec(3) tdec(2) e(r2_a,F) addstat(F test,e(p)) 
**/

* 制作附录表
use 回归数据.dta,clear
merge 1:1 Stkcd year using DA_计算结果.dta,keep(match) nogen
keep Stkcd year _b_A _b_REV _b_PPE DA AD ROA ROE LEV SIZE NK、
order Stkcd year _b_A _b_REV _b_PPE DA AD ROA ROE LEV SIZE NK
save 附录表.dta, replace
export excel 附录表.xlsx, firstrow(variable) replace